plugins {
	id 'org.springframework.boot' version '2.5.0'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id "io.freefair.lombok" version "6.0.0-m2"
	id "org.openapi.generator" version "5.1.1"
}

group = 'car.hey'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '15'

ext {
	swagger_annotations_version = "1.6.2"
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springdoc:springdoc-openapi-ui:1.5.2'
	implementation 'io.springfox:springfox-swagger2:3.0.0'
	implementation 'io.springfox:springfox-swagger-ui:3.0.0'
	implementation 'org.openapitools:jackson-databind-nullable:0.2.1'
	implementation 'org.mapstruct:mapstruct:1.4.2.Final'
	implementation 'com.opencsv:opencsv:5.2'

	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	runtimeOnly 'org.postgresql:postgresql'
    implementation 'org.liquibase:liquibase-core'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.4.2.Final'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	annotationProcessor "io.swagger:swagger-annotations:$swagger_annotations_version"

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

sourceSets {
	main {
		java {
			srcDirs+="$buildDir/generated/src/main/java"
		}
	}
}

test {
	useJUnitPlatform()
}

openApiGenerate {
    generatorName = "spring"
    inputSpec = "$rootDir/src/main/resources/api/garage-api.yml"
    outputDir = "$buildDir/generated"
    apiPackage = "car.hey.api"
    invokerPackage = "car.hey.api.invoker"
    modelPackage = "car.hey.api.model"
    library = "spring-boot"
	enablePostProcessFile = true
	skipOverwrite = false
    configOptions = [
        dateLibrary: "java8",
		serializationLibrary: "jackson",
		library             : "spring-boot",
		useBeanValidation   : "true",
		interfaceOnly       : "true",
		serializableModel   : "true",
		useTags             : "true"
    ]
}

compileJava.dependsOn tasks.openApiGenerate

compileJava {
    options.compilerArgs = [
            '-Amapstruct.defaultComponentModel=spring'
    ]
}